#!/usr/bin/env zsh
emulate -L zsh

local path count manager
setopt pushd_silent

# Supported managers
if [[ -v ZINIT_REGISTERED_PLUGINS ]]; then
	manager='Zinit'

	if pushd ${functions_source[zinit]:h} >/dev/null; then
		path="${(%):-%~}"
		popd -q
	fi
	count=$#ZINIT_REGISTERED_PLUGINS
elif [[ -v ZPLUG_ROOT ]]; then
	manager='zplug'

	if pushd $ZPLUG_ROOT >/dev/null; then
		path="${(%):-%~}"
		popd -q
	fi
	count=$#zplugs
elif [[ -v ZSH ]]; then
	manager='Oh My Zsh'

	if pushd $ZSH >/dev/null; then
		path="${(%):-%~}"
		popd -q
	fi
	count=$#plugins
else
	return 1
fi


# Manager
if zstyle -t :shellfetch:plugins show-manager && [[ -n $manager ]]; then
	local -a extra
	if zstyle -t :shellfetch:plugins show-path && [[ -n $path ]]; then
		extra+=($path)
	fi
	reply+=('Plugin Manager' "$manager ${extra:+"($extra)"}")
fi

# Count
if zstyle -T :shellfetch:plugins show-count && [[ -n $count ]]; then
	reply+=('Plugins' "$count")
fi
