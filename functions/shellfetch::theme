#!/usr/bin/env zsh
emulate -L zsh

# Save $reply, since cwd hooks can break things
local name location r=("${(@)reply}")
setopt pushd_silent

if [[ -v ZSH_THEME ]]; then
	# Oh-my-zsh theme
	name=$ZSH_THEME

	for location (
		"$ZSH_CUSTOM/$ZSH_THEME.zsh-theme"
		"$ZSH_CUSTOM/themes/$ZSH_THEME.zsh-theme"
		"$ZSH/themes/$ZSH_THEME.zsh-theme"
	); do
		[[ -f $location ]] && break
	done
	if pushd ${location:h} >/dev/null; then
		location="${(%):-%~}/${location:t}"
		popd -q
	fi
elif [[ -v P9K_PROMPT ]]; then
	if [[ -v functions[p10k] ]]; then
		name='Powerlevel10k'

		if pushd ${functions_source[p10k]:h} >/dev/null; then
			location="${(%):-%~}"
			popd -q
		fi
	else
		name='Powerlevel9k'

		if pushd $p9k_directory; then
			location=${(%):-%~}
			popd -q
		fi
	fi
else
	return 1
fi

local -a extra
if zstyle -t :shellfetch:theme show-path && [[ -n $location ]]; then
	extra+=($location)
fi
reply=("${(@)r}" 'Theme' "$name${extra:+" ($extra)"}")
